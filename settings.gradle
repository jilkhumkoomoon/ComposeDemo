pluginManagement {
    repositories {
        mavenLocal() //发布本地aar后，需要添加mavenLocal()才能引用本地发布的aar等文件
        maven{
            allowInsecureProtocol = true
            url 'http://dl.bintray.com/umsdk/release'
        }
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}

//打开versionCatalogs功能
//enableFeaturePreview('VERSION_CATALOGS')

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        mavenLocal() //发布本地aar后，需要添加mavenLocal()才能引用本地发布的aar等文件
        maven { url 'https://maven.aliyun.com/repository/releases' }
        maven { url 'https://maven.aliyun.com/repository/google' }
        maven { url 'https://maven.aliyun.com/repository/central' }
        maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
        maven { url 'https://maven.aliyun.com/repository/public' }
        maven { url 'https://www.jitpack.io' }
        google()
        mavenCentral()
    }

    versionCatalogs{
        create('androidxLibs') {
            //    implementation 'androidx.core:core-ktx:1.8.0'
            //    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
            //    implementation 'androidx.activity:activity-compose:1.5.1'

            //navigation
            library('navigation-fragment', 'androidx.navigation', 'navigation-fragment-ktx').version('2.5.3')
            library('navigation-ui', 'androidx.navigation', 'navigation-ui-ktx').version('2.5.3')

            library('material', 'com.google.android.material', 'material').version('1.5.0')

            library('core-ktx','androidx.core', 'core-ktx').version('1.8.0')
            library('lifecycle-runtime-ktx','androidx.lifecycle', 'lifecycle-runtime-ktx').version('2.3.1')
            library('activity-compose','androidx.activity', 'activity-compose').version('1.6.1')
            library('appcompat', 'androidx.appcompat', 'appcompat').version('1.4.1')
            library('constraintlayout', 'androidx.constraintlayout', 'constraintlayout').version('2.1.3')
            bundle('androidx', ['navigation-ui','navigation-fragment','material','constraintlayout','core-ktx','lifecycle-runtime-ktx','appcompat','activity-compose'])

            //retrofit+okhhtp
            library('retrofit', 'com.squareup.retrofit2', 'retrofit').version('2.9.0')
            library('converter-gson', 'com.squareup.retrofit2', 'converter-gson').version('2.9.0')
            library('okhttp', 'com.squareup.okhttp3', 'okhttp').version('4.9.1')
            library('logging', 'com.squareup.okhttp3', 'logging-interceptor').version('4.9.1')
            bundle('http', ['retrofit', 'converter-gson', 'okhttp', 'logging'])

            // nav-plugin
            library('asm', 'org.ow2.asm', 'asm').version('9.2')
            library('asm-tree', 'org.ow2.asm', 'asm-tree').version('9.2')
            library('commons-io', 'commons-io', 'commons-io').version('2.6')
            library('kotlinpoet', 'com.squareup', 'kotlinpoet').version('1.2.0')
            bundle('nav-plugin',['asm','asm-tree','commons-io','kotlinpoet'])

            //fastjson
            library('fastjson','com.alibaba','fastjson').version('1.2.59')
        }

        // Bom与Compose依赖库版本对应关系
        // https://developer.android.com/jetpack/compose/bom/bom-mapping?hl=zh-cn
        // 目前使用的是Bom:2023.01.00
        create('composeLibs') {
            //    implementation platform('androidx.compose:compose-bom:2022.10.00')
            //    implementation 'androidx.compose.ui:ui'
            //    implementation 'androidx.compose.ui:ui-graphics'
            //    implementation 'androidx.compose.ui:ui-tooling-preview'
            //    implementation 'androidx.compose.material3:material3'

            library('bom','androidx.compose','compose-bom').version('2023.01.00')

            library('material', 'androidx.compose.material', 'material').withoutVersion()
            library('ui', 'androidx.compose.ui', 'ui').withoutVersion()
            library('tooling','androidx.compose.ui','ui-tooling').withoutVersion()
            library('preview', 'androidx.compose.ui', 'ui-tooling-preview').withoutVersion()

            library('activity-compose', 'androidx.activity', 'activity-compose').version('1.6.1')

            //
            library('icons', 'androidx.compose.material', 'material-icons-core').withoutVersion()
            library('icons-ext', 'androidx.compose.material', 'material-icons-extended').withoutVersion()

            // 加载网络图片
            // 普通图片
            library('coil','io.coil-kt','coil-compose').version('2.4.0')
            // gif图片
            library('coil-gif','io.coil-kt','coil-gif').version('2.4.0')
            // svg图片
            library('coil-svg','io.coil-kt','coil-svg').version('2.4.0')
            bundle('compose', ['coil','coil-gif','coil-svg','material', 'ui', 'preview','tooling','icons', 'activity-compose','icons-ext'])
        }

        /**
         * 在app/build.gradle中
         * compileSdk:buildsdk.versions.compilesdk.get().toInteger()
         */
        create('buildsdk') {
            version('compilesdk', '33')
            version('minisdk', '23')
            version('targetsdk', '33')
        }
    }
}
rootProject.name = "ComposeDemo"
include ':app'

include ':nav-plugin'
include ':nav-plugin-runtime'
